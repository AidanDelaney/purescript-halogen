module Halogen.Driver
  ( Driver
  , runUI
  ) where

import Prelude

import Control.Coroutine (await)
import Control.Coroutine.Stalling (($$?))
import Control.Coroutine.Stalling as SCR
import Control.Monad.Aff (Aff, runAff, forkAff)
import Control.Monad.Aff.AVar (AVar, AVAR, makeVar', putVar, takeVar, modifyVar)
import Control.Monad.Eff (Eff)
import Control.Monad.Eff.Class (liftEff)
import Control.Monad.Eff.Exception (error, throwException)
import Control.Monad.Error.Class (throwError)
import Control.Monad.Free (foldFree)
import Control.Monad.Rec.Class (forever)
import Control.Monad.Trans (lift)
import Control.Parallel.Class (par)

import Data.Map as M
import Data.Maybe (Maybe(..))
import Data.Lazy (force)

import DOM.HTML.Types (HTMLElement, htmlElementToNode)
import DOM.Node.Node (appendChild)

import Halogen.Component (Component, ComponentSlot, unComponent, unComponentSlot)
import Halogen.Data.OrdBox (OrdBox, unOrdBox)
import Halogen.Driver.State (DriverStateX, DriverState(..), unDriverStateX, initDriverState)
import Halogen.Effects (HalogenEffects)
import Halogen.HTML.Renderer.VirtualDOM (renderHTML')
import Halogen.Internal.VirtualDOM as V
import Halogen.Query.ChildQuery (ChildQuery, unChildQuery)
import Halogen.Query.EventSource (runEventSource)
import Halogen.Query.HalogenF (HalogenF(..), ParF(..), unPar)
import Halogen.Query.HalogenM (HalogenM(..))
import Halogen.Query.StateF (StateF(..))

-- | Type alias for driver functions generated by `runUI` - a driver takes an
-- | input of the query algebra (`f`) and returns an `Aff` that returns when
-- | query has been fulfilled.
type Driver f eff = f ~> Aff (HalogenEffects eff)

type DSL s f g eff p o = HalogenF s f g p o (Aff (HalogenEffects eff)) (HalogenM s f g p o (Aff (HalogenEffects eff)))

-- | This function is the main entry point for a Halogen based UI, taking a root
-- | component, initial state, and HTML element to attach the rendered component
-- | to.
-- |
-- | The returned "driver" function can be used to send actions and requests
-- | into the component hierarchy, allowing the outside world to communicate
-- | with the UI.
runUI
  :: forall f eff o
   . Component f (Aff (HalogenEffects eff)) o
  -> HTMLElement
  -> Aff (HalogenEffects eff) (Driver f eff)
runUI component element = _.driver <$> do
  fresh <- makeVar' 0
  var <- runComponent (const (pure unit)) fresh component
  dsx <- peekVar var
  unDriverStateX (\st -> do
    -- TODO: run initializer(s) after element is added to DOM?
    liftEff $ appendChild (htmlElementToNode st.node) (htmlElementToNode element)
    -- The record here is a hack around a skolem escape issue. If the typing
    -- rules for records change so this no longer works it may also be fixable
    -- with copious type annotations.
    pure { driver: evalF st.selfRef }) dsx

runComponent
  :: forall f eff o
   . (o -> Aff (HalogenEffects eff) Unit)
  -> AVar Int
  -> Component f (Aff (HalogenEffects eff)) o
  -> Aff (HalogenEffects eff) (AVar (DriverStateX f eff))
runComponent handler fresh = unComponent \component -> do
  keyId <- peekVar fresh
  modifyVar (_ + 1) fresh
  var <- initDriverState component handler keyId fresh
  unDriverStateX (render <<< _.selfRef) =<< peekVar var
  pure var

eval
  :: forall s f g eff p o
   . AVar (DriverState s f g eff p o)
  -> DSL s f g eff p o
  ~> Aff (HalogenEffects eff)
eval ref = case _ of
  State i -> do
    case i of
      Get k -> do
        DriverState { state } <- peekVar ref
        pure (k state)
      Modify f next -> do
        modifyVar (\(DriverState st) -> DriverState (st { state = f st.state })) ref
        render ref
        pure next
  Subscribe es next -> do
    let consumer = forever (lift <<< evalF ref =<< await)
    forkAff $ SCR.runStallingProcess (runEventSource es $$? consumer)
    pure next
  Lift q ->
    -- TODO: do we need to render here?
    q
  Halt msg ->
    throwError (error msg)
  GetSlots k -> do
    DriverState { children } <- peekVar ref
    pure $ k $ map unOrdBox $ M.keys children
  ChildQuery cq ->
    evalChildQuery ref cq
  Raise o a -> do
    DriverState { handler } <- peekVar ref
    handler o
    pure a
  Par p -> do
    unPar (\(ParF f x y) â†’ par f (evalM ref x) (evalM ref y)) p

evalChildQuery
  :: forall s f g eff p o
   . AVar (DriverState s f g eff p o)
  -> ChildQuery g (Aff (HalogenEffects eff)) p
  ~> Aff (HalogenEffects eff)
evalChildQuery ref = unChildQuery \p k -> do
  DriverState st <- peekVar ref
  case M.lookup (st.mkOrdBox p) st.children of
    Just var -> do
      dsx <- peekVar var
      k (unDriverStateX (\ds q -> evalF ds.selfRef q) dsx)
    Nothing -> throwError (error "Slot lookup failed for child query")

evalF
  :: forall s f g eff p o
   . AVar (DriverState s f g eff p o)
  -> f
  ~> Aff (HalogenEffects eff)
evalF ref q = do
  DriverState st <- peekVar ref
  case st.component.eval q of
    HalogenM fx -> foldFree (eval ref) fx

evalM
  :: forall s f g eff p o
   . AVar (DriverState s f g eff p o)
  -> HalogenM s f g p o (Aff (HalogenEffects eff))
  ~> Aff (HalogenEffects eff)
evalM ref (HalogenM q) = foldFree (eval ref) q

peekVar :: forall eff a. AVar a -> Aff (avar :: AVAR | eff) a
peekVar v = do
  a <- takeVar v
  putVar v a
  pure a

render
  :: forall s f g eff p o
   . AVar (DriverState s f g eff p o)
  -> Aff (HalogenEffects eff) Unit
render var = takeVar var >>= \(DriverState ds) -> do
  children <- makeVar' (M.empty :: M.Map (OrdBox p) (AVar (DriverStateX g eff)))
  let selfEval = evalF ds.selfRef
  vtree' <-
    renderHTML'
      (handleAff <<< selfEval)
      (renderChild selfEval ds.fresh ds.mkOrdBox ds.children children)
      (ds.component.render ds.state)
  node' <- liftEff $ V.patch (V.diff ds.vtree vtree') ds.node
  newChildren <- takeVar children
  -- TODO: diff children, deal with initializers/finalizers
  putVar var $
    DriverState
      { node: node'
      , vtree: vtree'
      , component: ds.component
      , state: ds.state
      , children: newChildren
      , mkOrdBox: ds.mkOrdBox
      , selfRef: ds.selfRef
      , handler: ds.handler
      , keyId: ds.keyId
      , fresh: ds.fresh
      }

renderChild
  :: forall f g eff p
   . (f ~> Aff (HalogenEffects eff))
  -> AVar Int
  -> (p -> OrdBox p)
  -> M.Map (OrdBox p) (AVar (DriverStateX g eff))
  -> AVar (M.Map (OrdBox p) (AVar (DriverStateX g eff)))
  -> ComponentSlot g (Aff (HalogenEffects eff)) p (f Unit)
  -> Aff (HalogenEffects eff) V.VTree
renderChild handler fresh mkOrdBox childrenIn childrenOut =
  unComponentSlot \p ctor k -> do
    var <- case M.lookup (mkOrdBox p) childrenIn of
      Just existing -> pure existing
      Nothing -> runComponent (handler <<< k) fresh (force ctor)
    modifyVar (M.insert (mkOrdBox p) var) childrenOut
    unDriverStateX (\st -> pure $ V.widget st.keyId st.node) =<< peekVar var

-- | TODO: we could do something more intelligent now this isn't baked into the
-- | virtual-dom rendering. Perhaps write to an avar when an error occurs...
-- | something other than a runtime exception anyway.
handleAff
  :: forall eff a
   . Aff (HalogenEffects eff) a
  -> Eff (HalogenEffects eff) Unit
handleAff = void <<< runAff throwException (const (pure unit))
